plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id("io.papermc.paperweight.userdev") version "1.7.2"
}

group = 'com.github.hanielcota'
version = '1.0-SNAPSHOT'
tasks.build.dependsOn 'shadowJar'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }

}

dependencies {
    paperweight.paperDevBundle("1.20.2-R0.1-SNAPSHOT")

    //connection pool
    implementation 'com.zaxxer:HikariCP:5.1.0'

    //jda - discordAPI
    implementation 'net.dv8tion:JDA:5.1.2'

    //command-framework for spigot
    implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

    //caffeine cache
    implementation("com.github.ben-manes.caffeine:caffeine:3.1.8")

    //annotations processor
    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

compileJava {
    options.encoding = "UTF-8"
}

shadowJar {
    relocate 'co.aikar.commands', 'com.github.hanielcota.reports.acf'
    relocate 'co.aikar.locales', 'com.github.hanielcota.reports.locales'
}


def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
